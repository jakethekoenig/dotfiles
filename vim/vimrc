" trigger `autoread` when files changes on disk
set autoread
autocmd FocusGained,BufEnter,CursorHold,CursorHoldI * if mode() != 'c' | checktime | endif
" notification after file change
autocmd FileChangedShellPost *
\ echohl WarningMsg | echo "File changed on disk. Buffer reloaded." | echohl None

let mapleader = "-"
let maplocalleader = "="

 " Neovim {{{
set dir=~/.vim/.cache
set undofile
set spellfile=~/.vim/spell/en.utf-8.add
if has('nvim')
    set runtimepath^=~/.vim runtimepath+=~/.vim/after
    let &packpath=&runtimepath
    set undodir=~/.vim/undodir_nvim
    " Nvim has a rendering problem with title
    set notitle
else
    set undodir=~/.vim/undodir
 endif
 " }}}

" python {{{
let g:python3_host_prog = '~/dotfiles/vim/venv/bin/python3'
" }}}

" Plugin Configuration {{{

lua require('debugger')

" ctrlp {{{
set wildignore+=*/node_modules/*,*pycache*,*/repos/*,*build*,*/.env/*,*/venv/*,*/benchmark_repos/*,*/spice_logs/*
let g:ctrlp_working_path_mode = '0'
let g:ctrlp_show_hidden = 1
let g:ctrlp_max_files = 0

let g:ctrlp_map = '<c-p>'
nnoremap <leader>r :CtrlPClearCache<CR>
let g:ctrlp_switch_buffer = 'Et'
let g:ctrlp_open_new_file = 'v'
let g:ctrlp_open_multiple_files = 'v'
" }}}

" vim-iced {{{
let g:iced_enable_default_key_mappings = v:true
let g:sexp_enable_insert_mode_mappings = 0
" }}}

" NerdTree {{{
command! Tree :NERDTree
nnoremap <F6> :NERDTreeToggle<CR>
nnoremap <F5> :UndotreeToggle<CR>
"}}}

" Goyo {{{
function! s:goyo_enter() abort
    "colorscheme darkblue
    Copilot disable
    set nocursorline
    set nocursorcolumn
    set spell
    iabbrev i I
    iabbrev u you
    iabbrev U You
    call Abbrevword("b", "be")
    call Abbrevword("y", "why")
    call Abbrevword("ab", "about")
    call Abbrevword("id", "I'd")
endfunction

function! s:goyo_leave() abort
    "colorscheme desert
    Copilot enable
    set nocursorline
    set cursorline
    set cursorcolumn
    set nospell
    iunabbrev i
    iunabbrev u
    iunabbrev U
    UnAbbrevWord b
    UnAbbrevWord y
    UnAbbrevWord ab
    UnAbbrevWord id
    q
endfunction

autocmd! User GoyoEnter nested call <SID>goyo_enter()
autocmd! User GoyoLeave nested call <SID>goyo_leave()
" }}}

" Ultisnips {{{
let g:UltiSnipsExpandTrigger="<tab>"
let g:UltiSnipsJumpForwardTrigger="<c-f>"
let g:UltiSnipsJumpBackwardTrigger="<c-z>"
let g:UltiSnipsEditSplit="vertical"
" }}}

" Rainbow parentheses {{{
let g:rbpt_colorpairs = [
    \ ['brown',       'RoyalBlue3'],
    \ ['Darkblue',    'SeaGreen3'],
    \ ['darkgray',    'DarkOrchid3'],
    \ ['darkgreen',   'firebrick3'],
    \ ['darkcyan',    'RoyalBlue3'],
    \ ['darkred',     'SeaGreen3'],
    \ ['darkmagenta', 'DarkOrchid3'],
    \ ['brown',       'firebrick3'],
    \ ['gray',        'RoyalBlue3'],
    \ ['black',       'SeaGreen3'],
    \ ['darkmagenta', 'DarkOrchid3'],
    \ ['Darkblue',    'firebrick3'],
    \ ['darkgreen',   'RoyalBlue3'],
    \ ['darkcyan',    'SeaGreen3'],
    \ ['red',         'firebrick3'],
    \ ]
let g:rbpt_max = 16
let g:rbpt_loadcmd_toggle = 0
au VimEnter * RainbowParenthesesToggle
au VimEnter * RainbowParenthesesLoadRound
au VimEnter * RainbowParenthesesLoadSquare
au VimEnter * RainbowParenthesesLoadBraces
" }}}

" ChatVim {{{
let g:llm_model="anthropic/claude-4-sonnet-20250514"
"let g:llm_model="claude-3-opus-20240229"

let g:markdown_fenced_languages = ['html', 'js=javascript', 'python', 'clojure', 'javascript', 'tsx=typescriptreact', 'typescriptreact', 'ts=typescript', 'typescript' ]
" }}}
" }}}

" Aesthetics {{{
syntax on

set number rnu
set cursorline cursorcolumn
set scrolloff=5
set hidden

" Tmux color fix
set background=dark
set t_Co=256
" Needed to backspace over eol on mac
set backspace=2
set noerrorbells

set smartcase ignorecase noincsearch
set path+=**5
set path+=~/notes/**
set suffixesadd+=.txt
set wildmenu

set autoread

set nosplitbelow splitright

set softtabstop=4
set autoindent expandtab
set shiftwidth=4

filetype plugin indent on
set noequalalways
colorscheme slate
"}}}

" Terminal Configuration {{{
nnoremap <C-x> :AdaptiveSplit<CR>:term<CR>
tnoremap <C-n> <C-\><C-n>
nnoremap <C-n> <Nop>
tnoremap <C-p> <C-\><C-n>:CtrlP<CR>
tnoremap <C-o> <C-\><C-n><C-o>
tnoremap <C-h> <C-\><C-n><C-w><C-h>
tnoremap <C-j> <C-\><C-n><C-w><C-j>
tnoremap <C-k> <C-\><C-n><C-w><C-k>
tnoremap <C-l> <C-\><C-n><C-w><C-l>
tnoremap <C-w>1 <C-\><C-n>1gt
tnoremap <C-w>2 <C-\><C-n>2gt
tnoremap <C-w>3 <C-\><C-n>3gt
tnoremap <C-w>4 <C-\><C-n>4gt
tnoremap <C-w>5 <C-\><C-n>5gt
tnoremap <C-w>6 <C-\><C-n>6gt
tnoremap <C-w> <C-\><C-n><C-w>
nnoremap <C-w>1 1gt
nnoremap <C-w>2 2gt
nnoremap <C-w>3 3gt
nnoremap <C-w>4 4gt
nnoremap <C-w>5 5gt
nnoremap <C-w>6 6gt

function! RenameTerminalBufferToCurrentCommand()

  " unable to access $HISTFILE from vim, so change this variable to your history file
  let l:historyFile = "~/.zsh_history"
  let l:mostRecentCommand = system("tail -1 " . l:historyFile . " | cut -f2- -d\\;")
  let l:mostRecentCommand = fnameescape(trim(l:mostRecentCommand))
  if (l:mostRecentCommand == "q" || l:mostRecentCommand == "quit" || l:mostRecentCommand == "exit")
      return
  endif


  " i prepend "term" for easy buffer searching, but feel free to delete
  let l:newFileName = "term " . l:mostRecentCommand

  " the keepalt stops :file from creating an alternative file (alt files are
  " annoying when buffer switching)
  " :file renames the buffer
  silent! execute "keepalt file " . l:newFileName

endfunction
tnoremap <Enter> <Enter><C-\><C-n>:call RenameTerminalBufferToCurrentCommand()<Enter>a

nnoremap <leader>p :new \| terminal python3 -q<CR>i

nnoremap <leader>c ?```<CR>jvNk$"ty:vnew<CR>i<Esc>"tp:w! temp.py<CR>
nnoremap <leader>i :let currentfile = @% \| new \| execute 'terminal python3 -q -i '.currentfile<CR>i

"autocmd TermClose * if !v:event.status | silent! bdelete! | endif
"}}}

" Normal Mode Commands {{{
nnoremap <leader>f :enew \| setlocal buftype=nofile \| r !grep -r 

nnoremap <C-J> <C-W><C-J>
nnoremap <C-K> <C-W><C-K>
nnoremap <C-L> <C-W><C-L>
nnoremap <C-H> <C-W><C-H>

"function! MyGoToDef()
    "if (&filetype == 'python')
        "normal gd^w

        "let word = expand('<cword>')

    "endif
"nnoremap <leader>gd

nnoremap <C-v> "+p
inoremap <C-v> <Esc>"+pa
"}}}

" Skeletons {{{
augroup skeletons
	autocmd!
	autocmd BufNewFile *.sh 0r ~/.vim/skeletons/bash.sh
augroup END
"}}}

" Extending Files {{{
nnoremap <leader>ev :AdaptiveSplit $MYVIMRC<cr>
nnoremap <leader>sv :source $MYVIMRC<cr>
nnoremap <leader>es :UltiSnipsEdit<cr>

nnoremap <leader>el :AdaptiveSplit ~/notes/mentat.wofl<CR>
nnoremap <leader>en :AdaptiveSplit ~/notes/notes.wofl<CR>
nnoremap <leader>ej :AdaptiveSplit ~/notes/journal.txt<CR>
nnoremap <leader>eq :AdaptiveSplit + ~/notes/gpt_qs.md<CR>

" Notes stuff
nnoremap <leader>t :AdaptiveSplit + todo<CR>
nnoremap <leader>x jmxk^f[lrx<esc>dd}P`x
nnoremap <leader>o o- [ ] 

" }}}

" Insert Mode Commands {{{
inoremap <C-J> <esc><C-W><C-J>
inoremap <C-K> <esc><C-W><C-K>
inoremap <C-L> <esc><C-W><C-L>
inoremap <C-H> <esc><C-W><C-H>
inoremap <c-u> <esc>lviwUwi
inoremap jk <esc>
vnoremap jk <esc>
cnoremap jk <C-f>
inoremap :w <esc>:w<cr>
"}}}

" Livewriting mode {{{
function! ToggleWritingMode() abort
	if !exists('#WriteMode#InsertLeave')
		augroup WriteMode
			autocmd!
			autocmd InsertLeave * w
		augroup END
	else
		augroup WriteMode
			autocmd!
		augroup END
	endif
endfunction
command! WriteLive call ToggleWritingMode()
" }}}

" Abbreviations {{{

" I'm always making these types
command! Wq :wq
command! WQ :wq
command! Q :q
command! W :w
command! Wqa :wqa

" TODO: handle suffixes? Have toggle to handle which to generate? -v for verb?
function! UnAbbrevword(...) abort
	let toabbr = a:1
	execute ("iunabbrev " . toabbr)
	execute ("iunabbrev " . toabbr."s")
	execute ("iunabbrev " . (toupper(toabbr[0]).toabbr[1:]))
	execute ("iunabbrev " . (toupper(toabbr[0]).toabbr[1:]."s"))
endfunction
command! -nargs=+ UnAbbrevWord call UnAbbrevword(<f-args>)
function! Abbrevword(...) abort
	let toabbr = a:1
	let target = join(a:000[1:]," ") 
	execute ("iabbrev " . toabbr ." ".target)
	execute ("iabbrev " . toabbr."s ".target."s")
	execute ("iabbrev " . (toupper(toabbr[0]).toabbr[1:])." ".toupper(target[0]) . target[1:])
	execute ("iabbrev " . (toupper(toabbr[0]).toabbr[1:]."s ".toupper(target[0]).target[1:]."s"))
endfunction
command! -nargs=+ AbbrevWord call Abbrevword(<f-args>)

" Probability
AbbrevWord distr distribution
AbbrevWord disted distributed
AbbrevWord rv random variable
AbbrevWord rV random Variable
AbbrevWord prob probability
AbbrevWord probly probably
AbbrevWord prbs probabilities
AbbrevWord whp with high probability
AbbrevWord rMT random matrix theory

"elsewhere Logic (Mathematical bent but not purely)
iabbrev w/ with
iabbrev W/ With
AbbrevWord ew elsewhere
AbbrevWord tf therefore
AbbrevWord wlog without loss of generality
AbbrevWord wrt with respect to
AbbrevWord te there exists
AbbrevWord fa for all
AbbrevWord lhs left hand side
AbbrevWord rhs right hand side
AbbrevWord defin definition
AbbrevWord defly definitely
AbbrevWord b4 before
AbbrevWord im I'm
AbbrevWord ive I've
AbbrevWord ill I'll
AbbrevWord fn function
AbbrevWord st such that
AbbrevWord iff if and only if
AbbrevWord sa such as
AbbrevWord ow otherwise
AbbrevWord resp respectively
AbbrevWord bc because
AbbrevWord otoh on the other hand
AbbrevWord rprv representative
AbbrevWord wo without
AbbrevWord wha whatever
AbbrevWord surj surjective
AbbrevWord inj injective
AbbrevWord bij bijective
AbbrevWord iso isomorphism

" Linear Algebra
AbbrevWord ind independent
AbbrevWord indy independently
AbbrevWord indc independence
AbbrevWord linind linearly independent
AbbrevWord codim codimension
AbbrevWord cok cokernel
AbbrevWord dmn dimension
AbbrevWord dmnl dimensional
AbbrevWord rel relationship

" General Math
AbbrevWord expy exponentially
AbbrevWord inj injective
AbbrevWord poly polynomial
AbbrevWord seq sequence
AbbrevWord ctns continuous
AbbrevWord dont don't
AbbrevWord cant can't
AbbrevWord mb maybe
AbbrevWord bt but
AbbrevWord wev whatever

" Analysis
AbbrevWord triineq triangle inequality
AbbrevWord tbt therefore by the
AbbrevWord ineq inequality

iabbrev ret return
AbbrevWord jmail jakethekoenig@gmail.com

" MTG
AbbrevWord gy graveyard
AbbrevWord bf battlefield

" }}}

" Complicated Functions written by ChatGPT {{{
" Function to yank markdown code block or inline code
function! YankInlineOrBlock(register) abort
    " Save current cursor position
    let l:save_pos = getpos('.')

    " Decide behavior based on current line's content
    if getline('.') =~ '\m^\s*```'
        echo "Invalid position for yi` command"
        return
    endif

    " Check for the presence of backticks on current line
    if getline('.') =~ '\m`'
        " Yank inside inline backticks and respect register
        execute "normal! " . a:register . "vi`y"
    else
        " Yank inside triple backtick block

        " Move cursor to the nearest opening triple backtick above
        let l:line = search('^\s*```', 'bnW')

        " Check if we're in a proper markdown block
        if getline(l:line) !~ '^\s*```'
            echo "Not inside a markdown code block"
            call setpos('.', l:save_pos)
            return
        endif

        " Move cursor to the start of the block content
        call cursor(l:line + 1, 1)

        " Yank until the closing triple backtick
        let l:end_line = search('^\s*```', 'nW')

        " Check if we found the closing triple backtick
        if getline(l:end_line) !~ '^\s*```'
            echo "Unmatched markdown code block"
            call setpos('.', l:save_pos)
            return
        endif

        " Put the block content into a register (default register " and for operations)
        let lines = getline(l:line + 1, l:end_line - 1)
        call setreg(a:register, lines, 'l')
        " Restore cursor position
        call setpos('.', l:save_pos)
    endif
endfunction

" Remap yi` to call the function, allowing for explicit register use
nmap <silent> yi` :call YankInlineOrBlock(v:register)<CR>

" Github
function! CopyGithubLink() abort
  " Save the current working directory
  let l:original_cwd = getcwd()

  " Change to the directory of the current file
  let l:buffer_dir = expand('%:p:h')
  execute 'cd' fnameescape(l:buffer_dir)

  " Get the current file path relative to the repo's root
  let l:file_path = system('git ls-files --full-name ' . shellescape(expand('%')))

  if v:shell_error
    echo "Not a git repository or file not tracked"
    execute 'cd' fnameescape(l:original_cwd)
    return
  endif
  let l:file_path = trim(l:file_path)

  " Get the line number
  let l:line_number = line(".")

  " Get the origin URL from git
  let l:origin_url = system("git config --get remote.origin.url")
  if v:shell_error
    echo "Not a git repository or no origin remote"
    execute 'cd' fnameescape(l:original_cwd)
    return
  endif

  " Get the current branch name
  let l:branch_name = system("git rev-parse --abbrev-ref HEAD")
  if v:shell_error
    echo "Could not determine the current branch"
    execute 'cd' fnameescape(l:original_cwd)
    return
  endif
  let l:branch_name = trim(l:branch_name)

  " Sanitizing the origin URL
  " Trim newline and other trailing whitespace
  let l:origin_url = trim(l:origin_url)

  " Convert SSH URL to HTTPS URL if needed
  if l:origin_url =~? '^git@'
    let l:origin_url = substitute(l:origin_url, '^git@\(.*\):\(.*\)$', 'https://\1/\2', '')
  endif

  " Remove ".git" suffix if present
  let l:origin_url = substitute(l:origin_url, '.git$', '', '')

  " Construct the URL to the specific line in the file
  let l:github_link = l:origin_url . '/blob/' . l:branch_name . '/' . l:file_path . '#L' . l:line_number

  " Copy the GitHub link to the system clipboard
  let @+ = l:github_link
  echo "Copied to clipboard: " . l:github_link

  " Restore the original working directory
  execute 'cd' fnameescape(l:original_cwd)
endfunction

" Create a command to call the function
command! CopyGithubLinkToClipboard call CopyGithubLink()
nnoremap <leader>gh :CopyGithubLinkToClipboard<CR>

" AdaptiveSplit {{{
" Function to decide split direction based on window width
function! AdaptiveSplit(...)
    let threshold = 120  " adjust this value to your preference
    let fullcmd = ''
    if winwidth(0) > threshold
        let fullcmd .= 'vsplit'
    else
        if winheight(0) > 20
            let fullcmd .= 'split'
        else
            let fullcmd .= 'e'
        endif
    endif
    if a:0 > 0
        let fullcmd .= ' ' . join(a:000, ' ')
    endif

    execute fullcmd
endfunction

" Create a command to use this function
command! -nargs=* AdaptiveSplit call AdaptiveSplit(<f-args>)
" }}}

" Copy shell output (gpt couldn't write this one :(
nnoremap yo mmgg^"pyiW`m?^\V<c-r>=escape(@p, '/\')<cr><cr>j^vNk$y`m:noh<cr>
nnoremap <leader>k mmgg^"pyiW`m?^\V<c-r>=escape(@p, '/\')<cr><cr>:noh<cr>
nnoremap <leader>j mmgg^"pyiW`m/^\V<c-r>=escape(@p, '/\')<cr><cr>:noh<cr>
" }}}
