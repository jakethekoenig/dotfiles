" trigger `autoread` when files changes on disk
set autoread
autocmd FocusGained,BufEnter,CursorHold,CursorHoldI * if mode() != 'c' | checktime | endif
" notification after file change
autocmd FileChangedShellPost *
\ echohl WarningMsg | echo "File changed on disk. Buffer reloaded." | echohl None

let mapleader = "-"
let maplocalleader = "="

 " Neovim {{{
set dir=~/.vim/.cache
set undofile
set spellfile=~/.vim/spell/en.utf-8.add
if has('nvim')
    set runtimepath^=~/.vim runtimepath+=~/.vim/after
    let &packpath=&runtimepath
    set undodir=~/.vim/undodir_nvim
    " Nvim has a rendering problem with title
    set notitle
else
    set undodir=~/.vim/undodir
 endif
 " }}}

" python {{{
let g:python3_host_prog = '/Users/jakekoenig/dotfiles/vim/venv/bin/python3'
" }}}

" Plugin Configuration {{{
" ctrlp {{{
set wildignore+=*/node_modules/*,*pycache*,*repos*,*build*,*/.env/*,*/venv/*,*/.venv/*,*/benchmark_repos/*
let g:ctrlp_working_path_mode = '0'
let g:ctrlp_show_hidden = 1

let g:ctrlp_map = '<c-p>'
nnoremap <leader>r :CtrlPClearCache<CR>
" }}}

" vim-iced {{{
let g:iced_enable_default_key_mappings = v:true
let g:sexp_enable_insert_mode_mappings = 0
" }}}

" NerdTree {{{
command! Tree :NERDTree
nnoremap <F6> :NERDTreeToggle<CR>
nnoremap <F5> :UndotreeToggle<CR>
"}}}

" Goyo {{{
function! s:goyo_enter() abort
    "colorscheme darkblue
    Copilot disable
    set nocursorline
    set nocursorcolumn
    set spell
    iabbrev i I
    call Abbrevword("b", "be")
    call Abbrevword("u", "you")
    call Abbrevword("y", "why")
    call Abbrevword("ab", "about")
    call Abbrevword("id", "I'd")
endfunction

function! s:goyo_leave() abort
    "colorscheme desert
    Copilot enable
    set nocursorline
    set cursorline
    set cursorcolumn
    set nospell
    iunabbrev i
    UnAbbrevWord b
    UnAbbrevWord u
    UnAbbrevWord y
    UnAbbrevWord ab
    UnAbbrevWord id
    q
endfunction

autocmd! User GoyoEnter nested call <SID>goyo_enter()
autocmd! User GoyoLeave nested call <SID>goyo_leave()
" }}}

" Ultisnips {{{
let g:UltiSnipsExpandTrigger="<tab>"
let g:UltiSnipsJumpForwardTrigger="<c-f>"
let g:UltiSnipsJumpBackwardTrigger="<c-z>"
let g:UltiSnipsEditSplit="vertical"
" }}}

" Rainbow parentheses {{{
let g:rbpt_colorpairs = [
    \ ['brown',       'RoyalBlue3'],
    \ ['Darkblue',    'SeaGreen3'],
    \ ['darkgray',    'DarkOrchid3'],
    \ ['darkgreen',   'firebrick3'],
    \ ['darkcyan',    'RoyalBlue3'],
    \ ['darkred',     'SeaGreen3'],
    \ ['darkmagenta', 'DarkOrchid3'],
    \ ['brown',       'firebrick3'],
    \ ['gray',        'RoyalBlue3'],
    \ ['black',       'SeaGreen3'],
    \ ['darkmagenta', 'DarkOrchid3'],
    \ ['Darkblue',    'firebrick3'],
    \ ['darkgreen',   'RoyalBlue3'],
    \ ['darkcyan',    'SeaGreen3'],
    \ ['red',         'firebrick3'],
    \ ]
let g:rbpt_max = 16
let g:rbpt_loadcmd_toggle = 0
au VimEnter * RainbowParenthesesToggle
au VimEnter * RainbowParenthesesLoadRound
au VimEnter * RainbowParenthesesLoadSquare
au VimEnter * RainbowParenthesesLoadBraces
" }}}

" ChatVim {{{
let g:gpt_model="claude-3-opus-20240229"
" }}}
" }}}

" Aesthetics {{{
syntax on
"colorscheme desert

set number rnu
set cursorline cursorcolumn
set scrolloff=5

" Tmux color fix
set background=dark
set t_Co=256
" Needed to backspace over eol on mac
set backspace=2
set noerrorbells

set smartcase ignorecase noincsearch
set path+=**3
set wildmenu

set autoread

set nosplitbelow splitright

set softtabstop=4
set autoindent expandtab
set shiftwidth=4

filetype plugin indent on

let g:markdown_fenced_languages = ['html', 'js=javascript', 'python', 'clojure', 'javascript']
"}}}

" Terminal Configuration {{{
nnoremap <C-x> :vsp<CR>:term<CR>
tnoremap <C-x> <C-\><C-n>
tnoremap <C-p> <C-\><C-n>:CtrlP<CR>
tnoremap <C-o> <C-\><C-n><C-o>
tnoremap <C-h> <C-\><C-n><C-w><C-h>
tnoremap <C-j> <C-\><C-n><C-w><C-j>
tnoremap <C-k> <C-\><C-n><C-w><C-k>
tnoremap <C-l> <C-\><C-n><C-w><C-l>

function! RenameTerminalBufferToCurrentCommand()

  " unable to access $HISTFILE from vim, so change this variable to your history file
  let l:historyFile = "~/.zsh_history"
  let l:mostRecentCommand = system("tail -1 " . l:historyFile . " | cut -f2- -d\\;")
  let l:mostRecentCommand = fnameescape(trim(l:mostRecentCommand))
  if (l:mostRecentCommand == "q" || l:mostRecentCommand == "quit" || l:mostRecentCommand == "exit")
      return
  endif


  " i prepend "term" for easy buffer searching, but feel free to delete
  let l:newFileName = "term " . l:mostRecentCommand

  " the keepalt stops :file from creating an alternative file (alt files are
  " annoying when buffer switching)
  " :file renames the buffer
  silent! execute "keepalt file " . l:newFileName

endfunction
tnoremap <Enter> <Enter><C-\><C-n>:call RenameTerminalBufferToCurrentCommand()<Enter>a

nnoremap <leader>m :let currentfile = @% \| new \| execute 'terminal mentat '.currentfile<CR>i
nnoremap <leader>t :new \| terminal mentat<CR>i
nnoremap <leader>p :new \| terminal python3<CR>i

nnoremap <leader>c ?```<CR>jvNk$"ty:vnew<CR>i<Esc>"tp:w! temp.py<CR>
nnoremap <leader>i :let currentfile = @% \| new \| execute 'terminal python -i '.currentfile<CR>i

"autocmd TermClose * if !v:event.status | silent! bdelete! | endif
"}}}

" Normal Mode Commands {{{
nnoremap <leader>f :enew \| setlocal buftype=nofile \| r !grep -r 

nnoremap <C-J> <C-W><C-J>
nnoremap <C-K> <C-W><C-K>
nnoremap <C-L> <C-W><C-L>
nnoremap <C-H> <C-W><C-H>

nnoremap <C-v> "+p
inoremap <C-v> <Esc>"+pa
"}}}

" Skeletons {{{
augroup skeletons
	autocmd!
	autocmd BufNewFile *.sh 0r ~/.vim/skeletons/bash.sh
augroup END
"}}}

" Extending Files {{{
nnoremap <leader>ev :e $MYVIMRC<cr>
nnoremap <leader>sv :source $MYVIMRC<cr>
nnoremap <leader>es :UltiSnipsEdit<cr>

nnoremap <leader>el :e ~/notes/mentat.wofl<CR>
nnoremap <leader>en :e ~/notes/notes.wofl<CR>
nnoremap <leader>ej :e ~/notes/journal.txt<CR>
nnoremap <leader>eq :e + ~/notes/gpt_qs.md<CR>
" }}}

" Insert Mode Commands {{{
inoremap <C-J> <esc><C-W><C-J>
inoremap <C-K> <esc><C-W><C-K>
inoremap <C-L> <esc><C-W><C-L>
inoremap <C-H> <esc><C-W><C-H>
inoremap <c-u> <esc>lviwUwi
inoremap jk <esc>
vnoremap jk <esc>
cnoremap jk <C-f>
inoremap :w <esc>:w<cr>
"}}}

" Livewriting mode {{{
function! ToggleWritingMode() abort
	if !exists('#WriteMode#InsertLeave')
		augroup WriteMode
			autocmd!
			autocmd InsertLeave * w
		augroup END
	else
		augroup WriteMode
			autocmd!
		augroup END
	endif
endfunction
command! WriteLive call ToggleWritingMode()
" }}}

" Abbreviations {{{

" I'm always making these types
command! Wq :wq
command! WQ :wq
command! Q :q
command! W :w
command! Wqa :wqa

" TODO: handle suffixes? Have toggle to handle which to generate? -v for verb?
function! UnAbbrevword(...) abort
	let toabbr = a:1
	execute ("iunabbrev " . toabbr)
	execute ("iunabbrev " . toabbr."s")
	execute ("iunabbrev " . (toupper(toabbr[0]).toabbr[1:]))
	execute ("iunabbrev " . (toupper(toabbr[0]).toabbr[1:]."s"))
endfunction
command! -nargs=+ UnAbbrevWord call UnAbbrevword(<f-args>)
function! Abbrevword(...) abort
	let toabbr = a:1
	let target = join(a:000[1:]," ") 
	execute ("iabbrev " . toabbr ." ".target)
	execute ("iabbrev " . toabbr."s ".target."s")
	execute ("iabbrev " . (toupper(toabbr[0]).toabbr[1:])." ".toupper(target[0]) . target[1:])
	execute ("iabbrev " . (toupper(toabbr[0]).toabbr[1:]."s ".toupper(target[0]).target[1:]."s"))
endfunction
command! -nargs=+ AbbrevWord call Abbrevword(<f-args>)

" Probability
AbbrevWord distr distribution
AbbrevWord disted distributed
AbbrevWord rv random variable
AbbrevWord rV random Variable
AbbrevWord prob probability
AbbrevWord probly probably
AbbrevWord prbs probabilities
AbbrevWord whp with high probability
AbbrevWord rMT random matrix theory

"elsewhere Logic (Mathematical bent but not purely)
iabbrev w/ with
iabbrev W/ With
AbbrevWord ew elsewhere
AbbrevWord tf therefore
AbbrevWord wlog without loss of generality
AbbrevWord wrt with respect to
AbbrevWord te there exists
AbbrevWord fa for all
AbbrevWord lhs left hand side
AbbrevWord rhs right hand side
AbbrevWord defin definition
AbbrevWord defly definitely
AbbrevWord b4 before
AbbrevWord im I'm
AbbrevWord ive I've
AbbrevWord ill I'll
AbbrevWord fn function
AbbrevWord st such that
AbbrevWord iff if and only if
AbbrevWord sa such as
AbbrevWord ow otherwise
AbbrevWord resp respectively
AbbrevWord bc because
AbbrevWord otoh on the other hand
AbbrevWord rprv representative
AbbrevWord wo without
AbbrevWord wha whatever
AbbrevWord surj surjective
AbbrevWord inj injective
AbbrevWord bij bijective
AbbrevWord iso isomorphism

" Linear Algebra
AbbrevWord ind independent
AbbrevWord indy independently
AbbrevWord indc independence
AbbrevWord linind linearly independent
AbbrevWord codim codimension
AbbrevWord cok cokernel
AbbrevWord dmn dimension
AbbrevWord dmnl dimensional
AbbrevWord rel relationship

" General Math
AbbrevWord expy exponentially
AbbrevWord inj injective
AbbrevWord poly polynomial
AbbrevWord seq sequence
AbbrevWord ctns continuous
AbbrevWord dont don't
AbbrevWord cant can't
AbbrevWord mb maybe
AbbrevWord bt but
AbbrevWord wev whatever

" Analysis
AbbrevWord triineq triangle inequality
AbbrevWord tbt therefore by the
AbbrevWord ineq inequality

iabbrev ret return
AbbrevWord jmail jakethekoenig@gmail.com

" MTG
AbbrevWord gy graveyard
AbbrevWord bf battlefield

" }}}
